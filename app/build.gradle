plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 30

    defaultConfig {
        applicationId "com.medetzhakupov"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner 'com.medetzhakupov.DexOpenerTestRunner'
    }

    sourceSets {
        def commonTestDir = 'src/commonTest/java'
        def commonTestResourcesDir = 'src/commonTest/resources'

        test {
            java.srcDirs += [commonTestDir]
            resources.srcDirs += [commonTestResourcesDir]
        }

        androidTest {
            java.srcDirs += [commonTestDir]
            resources.srcDirs += [commonTestResourcesDir]
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }

        debug {

        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.returnDefaultValues = true
    }
}

def mockitoVersion = "3.3.0"
def fragment_version = "1.3.4"
def moshi_version = "1.11.0"
def retrofit_version = "2.9.0"

dependencies {

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.5.0'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'com.google.android.material:material:1.3.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    implementation 'androidx.lifecycle:lifecycle-livedata-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    implementation "androidx.recyclerview:recyclerview:1.2.0"

    implementation "com.squareup.moshi:moshi:$moshi_version"
    implementation "com.squareup.moshi:moshi-kotlin:$moshi_version"
    implementation 'com.squareup.picasso:picasso:2.71828'
    implementation "com.squareup.retrofit2:retrofit:$retrofit_version"
    implementation "com.squareup.retrofit2:converter-moshi:$retrofit_version"
    // Note that espresso-idling-resource is used in the code under test.
    implementation 'androidx.test.espresso:espresso-idling-resource:3.3.0'

    // Testing Fragments in Isolation
    debugImplementation "androidx.fragment:fragment-testing:$fragment_version"
    testImplementation 'junit:junit:4.13.2'
    testImplementation "org.assertj:assertj-core:3.18.0"
    testImplementation 'org.jetbrains.kotlinx:kotlinx-coroutines-test:1.4.2'
    testImplementation "org.mockito:mockito-core:${mockitoVersion}"
    testImplementation "org.mockito:mockito-inline:${mockitoVersion}"
    testImplementation "org.mockito:mockito-android:${mockitoVersion}"
    testImplementation "org.mockito.kotlin:mockito-kotlin:3.1.0"
    testImplementation 'androidx.arch.core:core-testing:2.1.0'

    androidTestImplementation "org.mockito:mockito-core:${mockitoVersion}"
    androidTestImplementation ("androidx.arch.core:core-testing:2.1.0") {
        // Due to issue here: https://github.com/mockito/mockito/issues/2007
        // This workaround can be removed as soon as this commit of mockito is released:
        // https://github.com/PaulKlauser/mockito/commit/e520d81468aacffb3fa09338329cacfb51d524b4
        exclude group: "org.mockito", module: "mockito-core"
    }
    androidTestImplementation "org.mockito:mockito-android:${mockitoVersion}"
    androidTestImplementation("org.mockito.kotlin:mockito-kotlin:3.1.0") {
        // Due to issue here: https://github.com/mockito/mockito/issues/2007
        // This workaround can be removed as soon as this commit of mockito is released:
        // https://github.com/PaulKlauser/mockito/commit/e520d81468aacffb3fa09338329cacfb51d524b4
        exclude group: "org.mockito", module: "mockito-core"
    }
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation 'com.github.tmurakami:dexopener:2.0.5'
    androidTestImplementation "androidx.test.espresso:espresso-contrib:3.3.0"
}